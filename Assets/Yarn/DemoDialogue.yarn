title: Start
tags:
---
<<declare $GooseIntroduced = false>>
<<declare $NoticedSandwich = false>>
<<declare $HasHandkerchief = false>>
<<declare $InspectHandkerchief = false>>

<<ChangeLeftCharacter Nancy>>
<<ChangeRightCharacter>>
Nancy: It's only noon and it's already been a long day. 
Nancy: *sigh*
Nancy: I'm just excited that it's my lunch break.
===

title: Goose
---
<<ChangeRightCharacter Goose>>

<<if $GooseIntroduced is false>>
	<<jump GooseIntroduction>>
<<else>>
	<<jump GooseIrritated>>
<<endif>>
===

title: GooseIntroduction
---
<<set $GooseIntroduced to true>>
Nancy: Hello Mr. Gander.
Chase: Hello Nancy.
===

title: GooseIrritated
---
Chase: Get on with it already.
Nancy: Ok ok...
===

title: GooseSuspicion
---
<<ChangeRightCharacter Goose>>
-> Talk
	Nancy: Mr. Gander, did you eat my sandwich?
	Chase: There is nothing so foul as eating someone else's lunch. I demand you take back that accusation.
	Nancy: I'll find proof. It's around here somewhere.
-> Present Evidence <<if $InspectHandkerchief>>
	-> Present Handkerchief
		<<jump GooseAccusation>>
===

title: GooseAccusation
---
Nancy: Tell me Chase, have you ever seen this Handkerchief before?
Chase: Never. It looks far too dirty for one I would own.
Nancy: Dirtied by a jelly stain, in fact.
Nancy: I also noticed some initials in the corner… CGW… Does that not fit your name? 
Chase: Could be anyone.
Nancy: Chase - Gander - Wilde. Quite the coincidence isn't it?
Chase: If it was mine, what of it?
Nancy: Chase Wilde Gander, did you eat my sandwich?
Chase: …
Nancy: What do you have to say for yourself?
Chase: It was delicious and I'd do it again.
===

title: MissingSandwich
---
<<ChangeRightCharacter>>
<<if $NoticedSandwich is false>>
	Nancy: My sandwich!
	Nancy: My peanut butter and jelly sandwich!
	Nancy: Who would do such a thing?
	<<set $NoticedSandwich to true>>
	<<set_node Goose GooseSuspicion>>
<<else>>
	Nancy: My sandwich...
<<endif>>
===

title: FoundHandkerchief
---
<<ChangeRightCharacter>>
<<set $HasHandkerchief to true>>
Nancy: Aha!
Nancy: A handkerchief!
	-> Inspect
		<<set $InspectHandkerchief to true>>
		Nancy: There are jelly stains on the handkerchief! The perpetrator must have used this!
		Nancy: And the initials: CGW.
		Nancy: Those seem very familiar...
===